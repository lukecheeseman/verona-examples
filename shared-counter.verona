/* Shared counter
 *
 * A counter shared between two turnstiles
 * - shows that all increments are atomic and not increment is lost
 *
 */

class Turnstile {
  _people: cown[U64Obj] & imm;

  create(people: cown[U64Obj] & imm): Turnstile & iso {
    var result = new Turnstile;
    result._people = people;
    result
  }

  run(self: iso) {
    var i = 0;
    while (i < 10) {
      when (var c = self._people) {
        c.v = c.v + 1;
        Builtin.print1("Counter value: {:#}\n", c.v);
      };
      i = i + 1
    }
  }
}

class Garden {
  go() {
    var counter = cown.create(U64Obj.create(0));
    var east = Turnstile.create(counter);
    var west = Turnstile.create(counter);

    when () {
      east.run();
    };

    when () {
      west.run();
    };
  }
}

class Main {
  main() {
    Garden.go()
  }
}
