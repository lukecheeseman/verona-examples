/* Concert hall boooking example
 *
 * A central computer holds three seats that can be reserved once.
 *
 * A terminal holds a reference to a cown of a central computer, the terminal
 * can attempt to book a seat and reports whether the reservation was succesful.
 *
 * The example creates two terminals that share a central computer, the
 * two terminals run asyncrhonously and both try to book all three seats.
 */

use "../common/bool.verona"

class Seat {
  // reserved is a boolean value that can only be set once
  _reserved: Bool & imm;

  create(): Seat & iso {
    var result = new Seat;
    result._reserved = Bool.false();
    result
  }

  // reserve the seat and report whether it was sucessfully reserved
  reserve(self: mut): Bool & imm {
    (self._reserved = Bool.true()).lnot()
  }
}

// Central computer has three seats, currently there are no maps
// so the seats are 3 fields.
class CentralComputer {
  _s1: Seat & iso;
  _s2: Seat & iso;
  _s3: Seat & iso;

  create(): cown[CentralComputer] & imm {
    var result = new CentralComputer;
    result._s1 = Seat.create();
    result._s2 = Seat.create();
    result._s3 = Seat.create();
    cown.create(result)
  }

  // attempt to reserve a seat and report wether it was succesfully
  // reserved or not
  reserve(self: mut, seat: U64 & imm): Bool & imm {
    if seat == 1 {
      (mut-view (self._s1)).reserve()
    } else {
      if seat == 2{
        (mut-view (self._s2)).reserve()
      } else {
        if seat == 3 {
          (mut-view (self._s3)).reserve()
        } else {
          Bool.false()
        }
      }
    }
  }
}

// Terminal attempts to make bookings on a central computer and reports whether
// a seat reservation was succesful.
class Terminal {
  _computer: cown[CentralComputer] & imm;
  _id: U64 & imm;

  create(id: U64 & imm, computer: cown[CentralComputer] & imm): Terminal & iso {
    var result = new Terminal;
    result._computer = computer;
    result._id = id;
    result
  }

  reserve(self: mut, seat: U64 & imm) {
    var tid = self._id;
    when (var c = self._computer) {
      if (((mut-view c).reserve(seat)).apply()) {
        Builtin.print2("Terminal {:#} reserved {:#}\n", tid, seat);
      } else {
        Builtin.print2("Terminal {:#} failed to reserved {:#}\n", tid, seat);
      }
    }
  }
}

// TODO: i think this example is broken an that one terminal will always book all 3 seats
//

// Build the example out of two terminals that share a central computer. The
// terminals run asynchronously and both try to book all three seats.
class Main {
  main() {
    var cc = CentralComputer.create();
    var t1 = Terminal.create(1, cc);
    var t2 = Terminal.create(2, cc);

    when() {
      (mut-view t1).reserve(1);
      (mut-view t1).reserve(2);
      (mut-view t1).reserve(3);
    };

    when() {
      (mut-view t2).reserve(3);
      (mut-view t2).reserve(2);
      (mut-view t2).reserve(1);
    };
  }
}
