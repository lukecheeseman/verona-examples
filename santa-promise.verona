class Node[T: iso | mut] {
  value: T | (None & imm);
  next: (Node[T] & mut) | (None & imm);
}

class List[T: iso | mut] {
  _head: (Node[T] & mut) | (None & imm);
  _size: U64;
  _none: (None & imm);

  _init(self: mut) {
    self._none = freeze (new None);
    self._head = self._none;
    self._size = 0;
  }

  create(): List[T] & iso {
    var result = new List;
    (mut-view result)._init();
    result
  }

  create_in(parent: mut): List[T] & mut {
    var result = new List in parent;
    result._init();
    result
  }

  push(self: mut, value: T) {
    self._head = self._push(self._head = self._none, value);
    self._size = self._size + 1;
  }

  _push(self: mut,
          current: (Node[T] & mut) | (None & imm),
          value: T): (Node[T] & mut) {
    match current
    {
      var e: None => {
        var node = new Node in self;
        node.value = value;
        node.next = self._none;
        node
      },
      var n: Node[T] => {
        n.next = self._push(n.next = self._none, value);
        n
      }
    }
  }

  pop(self: mut): T | (None & imm) {
    match (self._head = self._none) {
      var e: None => e,
      var n: Node[T] => {
        self._head = (n.next = self._none);
        self._size = self._size - 1;
        n.value = self._none
      }
    }
  }

  size(self: mut): U64 {
    self._size
  }
}

class ReindeerGroup {
  _capacity: U64;
  _data: List[Reindeer & iso] & iso;
  _promise: Promise[List[Reindeer & iso]] & iso;
  _santa: cown[Santa];

  _reindeer_deliver(v: (Reindeer & iso) | (None & imm)) {
    match v  {
      var n: None => None,
      var r: Reindeer => {
        r = r.deliver_toys();
        r.graze();
      }
    };
  }

  _create_promise(self: mut): Promise[List[Reindeer & iso]] & iso {
    var p = Promise.create();
    var g = (mut-view p).wait_handle();
    when (var s = self._santa, g) {
      s.deliver_toys();
      while g.size() > 0 {
        ReindeerGroup._reindeer_deliver(g.pop());
      };
      Builtin.print("=============================\n");
    };
    p
  }

  create(capacity: U64, santa: cown[Santa]): ReindeerGroup & iso {
    var result = new ReindeerGroup;
    result._capacity = capacity;
    result._data = List.create();
    result._santa = santa;
    result._promise = (mut-view result)._create_promise();
    result
  }

  full(self: mut): U64 {
    (mut-view (self._data)).size() >= self._capacity
  }

  add(self: mut, value: Reindeer & iso) {
    (mut-view (self._data)).push(value);
    if self.full() {
      var p = (self._promise = self._create_promise());
      p.fulfill(self._data = List.create())
    } else {
    }
  }

  remove(self: mut): (Reindeer & iso) | (None & imm) {
    (mut-view (self._data)).pop()
  }

  capacity(self: iso): U64 {
    self._capacity
  }
}

class Reindeer {
  _group: cown[ReindeerGroup];
  _id: U64;

  create(group: cown[ReindeerGroup], id: U64) {
    var result = new Reindeer;
    result._group = group;
    result._id = id;
    result.ready();
  }

  ready(self: iso) {
    when (var g = self._group) {
      g.add(self);
    }
  }

  graze(self: iso) {
    // This should be some sleep
    when() {
      var i = 0;
      while (i < 2000000) {
        i = i + 1;
      };
      self.ready();
    }
  }

  deliver_toys(self: iso): Reindeer & iso {
    Builtin.print1("Reindeer {:#} is delivering toys\n", self._id);
    self
  }
}

class ElfGroup {
  _capacity: U64;
  _data: List[Elf & iso] & iso;
  _promise: Promise[List[Elf & iso]] & iso;
  _santa: cown[Santa];

  _elf_meet(v: (Elf & iso) | (None & imm)) {
    match v  {
      var n: None => None,
      var e: Elf => {
        e = e.meet_in_study();
        e.holiday();
      }
    };
  }

  _create_promise(self: mut): Promise[List[Elf & iso]] & iso {
    var p = Promise.create();
    var g = (mut-view p).wait_handle();
    when (var s = self._santa, g) {
      s.meet_in_study();
      while g.size() > 0 {
        ElfGroup._elf_meet(g.pop());
      };
      Builtin.print("=============================\n");
    };
    p
  }

  create(capacity: U64, santa: cown[Santa]): ElfGroup & iso {
    var result = new ElfGroup;
    result._capacity = capacity;
    result._data = List.create();
    result._santa = santa;
    result._promise = (mut-view result)._create_promise();
    result
  }

  full(self: mut): U64 {
    (mut-view (self._data)).size() >= self._capacity
  }

  add(self: mut, value: Elf & iso) {
    (mut-view (self._data)).push(value);
    if self.full() {
      var p = (self._promise = self._create_promise());
      p.fulfill(self._data = List.create())
    } else {}
  }

  remove(self: mut): (Elf & iso) | (None & imm) {
    (mut-view (self._data)).pop()
  }

  capacity(self: iso): U64 {
    self._capacity
  }
}

class Elf {
  _group: cown[ElfGroup];
  _id: U64;

  create(group: cown[ElfGroup], id: U64) {
    var result = new Elf;
    result._group = group;
    result._id = id;
    result.ready();
  }

  ready(self: iso) {
    when (var g = self._group) {
      g.add(self);
    }
  }

  holiday(self: iso) {
    // This should be some sleep
    when() {
      var i = 0;
      while (i < 2000000) {
        i = i + 1;
      };
      self.ready();
    }
  }

  meet_in_study(self: iso): Elf & iso {
    Builtin.print1("Elf {:#} is meeting in study\n", self._id);
    self
  }
}

class Santa {
  create(): Santa & iso {
    new Santa
  }

  deliver_toys(self: mut) {
    Builtin.print("Santa is delivering toys\n");
  }

  meet_in_study(self: mut) {
    Builtin.print("Santa is meeting in study\n");
  }
}

class Workshop {
  run() {
    var santa = cown(Santa.create());

    var rg = cown(ReindeerGroup.create(9, santa));

    var i = 0;
    while i < 9 {
      Reindeer.create(rg, i);
      i = i + 1;
    };

    /*
    var eg = cown(ElfGroup.create(9, santa));
    i = 0;
    while i < 9 {
      Elf.create(eg, i);
      i = i + 1;
    }*/
  }
}

class Main {
  main() {
    Workshop.run();
  }
}
