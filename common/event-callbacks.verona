use "../common/list.verona"
use "../common/callbacks.verona"

/*
 * An Event is a list of waiting callbacks.
 * Calling await enqueues a new callback.
 * Calling signal dequeus and runs all waiting callbacks.
 */
class Event {
  _waiters: Queue[CallbackNode] & iso;

  create(): Event & iso {
    var result = new Event;
    result._waiters = Queue.create(0);
    result
  }

  // create a new waiting call back
  await(self: mut, callback: Callback & iso) {
    (mut-view (self._waiters)).add(CallbackNode.create(callback));
  }

  _run_queue(self: mut) {
    match (mut-view (self._waiters)).remove() {
      var _: None => {} // all waiters have started running
      var c: CallbackNode => {
        when() { c.apply(); };
        self._run_queue();
      }
    }
  }

  // dispatch all waiting callbacks
  signal(self: mut) {
    self._run_queue();
  }
}