use "list.verona"

/*
 * Example of how several behaviours can be scheduled to coordinate
 * and meet at a certain point.
 *
 * Main creates a Barrier, each Participant notifies a Barrier the
 * Participant is created. This increments the number of expected
 * Participants for a blocking call.
 *
 * Participants do some work (pre) then call block on the Barrier and
 * the Participant passes itself to the Barrier. A Barrier aggregates
 * the Participants together until all Participants have called block.
 *
 * Once all Participants have blocked, the Barreir will dispatch
 * all Participants to do some more work (post).
 */

class Participant {
  _barrier: cown[Barrier] & imm;
  _id: U64 & imm;

  create(id: U64 & imm, barrier: cown[Barrier] & imm): Participant & iso {
    var result = new Participant;
    Barrier.notify(barrier);
    result._barrier = barrier;
    result._id = id;
    result
  }

  // Some asynchronous work to do and then block, waiting for all Participants
  // to reach the same point.
  pre(self: iso) {
    when() {
      Builtin.print1("Pre: {:#}\n", self._id);
      Barrier.block(self._barrier, self);
    }
  }

  // Some asynchronous work to do afterwards
  post(self: iso) {
    when() {
      Builtin.print1("Post: {:#}\n", self._id);
    }
  }
}

class Barrier {
  _ps: Queue[Participant] & iso;
  _n: U64 & imm;

  create(): cown[Barrier] & imm {
    var result = new Barrier;
    result._ps = Queue.create(0);
    result._n = 0;
    cown.create(result)
  }

  // Increase the counter for expected participants to call block.
  notify(barrier: cown[Barrier] & imm) {
    when(barrier) {
      barrier._n = barrier._n + 1;
    }
  }

  // Call post on all blocked Participants.
  _dispatch(ps: Queue[Participant] & iso) {
    match((mut-view ps).remove()) {
      var _: None => {} // we're done
      var p: Participant => {
        p.post();
        Barrier._dispatch(ps);
      }
    }
  }

  // Block a Participant, decrementing the count for expected number
  // of Participants to call block. Once all Participants have called
  // block, dispatch all Partcipants.
  block(barrier: cown[Barrier] & imm, p: Participant & iso) {
    when(barrier) {
      (mut-view (barrier._ps)).add(p);
      barrier._n = barrier._n - 1;
      if (barrier._n == 0) {
        Barrier._dispatch(barrier._ps = Queue.create(0));
      }
    }
  }
}

class Main {
  main() {
    var barrier = Barrier.create();
    var p1 = Participant.create(1, barrier);
    var p2 = Participant.create(2, barrier);
    var p3 = Participant.create(3, barrier);
    var p4 = Participant.create(4, barrier);
    var p5 = Participant.create(5, barrier);

    p1.pre();
    p2.pre();
    p3.pre();
    p4.pre();
    p5.pre();
  }
}
